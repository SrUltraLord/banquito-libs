plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

group "io.github.srultralord"
version "0.1"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.projectlombok:lombok:1.18.24"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "io.github.srultralord"
            artifactId = "banquito-libs"
            version = "0.1"
            from components.java

            pom {
                name = "Banquito Libs"
                description = "Librer√≠as para los microservicios de Banquito"
                url = "https://github.com/SrUltraLord/banquito-libs"
                inceptionYear = '2022'

                scm {
                    connection = "scm:git:git:github.com/SrUltraLord/banquito-libs.git"
                    developerConnection = "scm:git:ssh://github.com/SrUltraLord/banquito-libs.git"
                    url = "https://github.com/SrUltraLord/banquito-libs"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username = project.properties["ossrhUsername"]
                password = project.properties["ossrhPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}